/*
 * Copyright 2022 Arunkumar
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: "maven-publish"
apply plugin: "signing"
apply plugin: "org.jetbrains.dokka"

task androidSourcesJar(type: Jar) {
  archiveClassifier.set("sources")
  if (project.plugins.findPlugin("com.android.library")) {
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.kotlin.srcDirs
  } else {
    from sourceSets.main.java.srcDirs
    from sourceSets.main.kotlin.srcDirs
  }
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
  archiveClassifier.set("javadoc")
  from dokkaJavadoc.outputDirectory
}

artifacts {
  archives androidSourcesJar
  archives javadocJar
}

afterEvaluate {
  publishing {
    publications {
      release(MavenPublication) {
        groupId project.findProperty("groupId")
        artifactId project.name
        version project.findProperty("publishVersion")

        if (project.plugins.findPlugin("com.android.library")) {
          from components.release
        } else {
          from components.java
        }

        artifact androidSourcesJar
        artifact javadocJar

        pom {
          name = project.name
          description = project.description
          url = website
          licenses {
            license {
              name = "Apache License, Version 2.0"
              url = "https://raw.githubusercontent.com/arunkumar9t2/scabbard/main/LICENSE"
            }
          }
          developers {
            developer {
              id = "arunkumar9t2"
              name = "Arunkumar"
              email = "hi@arunkumar.dev"
            }
          }
          scm {
            connection = "${website}.git"
            developerConnection = "${website}.git"
            url = website
          }
        }
      }
    }
  }
}

ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
  sign publishing.publications
}
