buildscript {
    apply from: 'constants.gradle'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${versions.agp}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

//TODO Investigate migrating to GradleBuild. ATM has issues disabling daemon.
tasks.register("runScabbardProcessor" /*, GradleBuild*/) {
    doLast {
        delete fileTree("app/build").matching {
            include "**/*.dot"
            include "**/*.png"
        }
        exec {
            def shell = System.getProperty("os.name").toLowerCase().contains("win") ? 'cmd' : 'sh'
            def isDebug = project.hasProperty("debug")
            def gradleDebug = "-Dorg.gradle.debug=$isDebug"
            def daemon = isDebug ? '--no-daemon' : ''
            workingDir(projectDir)
            commandLine shell, '/c', 'gradlew', daemon, gradleDebug, 'app:kaptDebugKotlin'
        }
    }
}