buildscript {
    apply from: 'constants.gradle'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${versions.agp}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "dev.arunkumar:scabbard-gradle-plugin:${publishVersion}"
        classpath 'com.novoda:bintray-release:0.9.2'
    }
}

apply from: "gradle/local-properties.gradle"

allprojects {
    repositories {
        google()
        jcenter()
    }
    group groupId
    version publishVersion
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def isWin = System.getProperty("os.name").toLowerCase().contains("win")

//TODO Investigate migrating to GradleBuild. ATM has issues disabling daemon.
tasks.register("runScabbardProcessor" /*, GradleBuild*/) {
    group = "development"
    def sample = "scabbard-sample"
    doLast {
        def isDebug = project.hasProperty("debug")
        if (isDebug) {
            delete fileTree("${sample}/build").matching {
                include "**/*.dot"
                include "**/*.png"
            }
        }
        exec {
            def shell = isWin ? 'cmd' : 'sh'
            def commandPrefix = isWin ? '' : './'
            def shellArg = isWin ? '/c' : '-c'

            def gradleDebug = "-Dorg.gradle.debug=$isDebug"
            def daemon = isDebug ? '--no-daemon' : ''
            def command = "${commandPrefix}gradlew $daemon $gradleDebug ${sample}:kaptDebugKotlin"
            workingDir(projectDir)
            commandLine shell, shellArg, command
        }
    }
}

tasks.register("docsServe") {
    group = "documentation"
    description = "Serve documentation website and open browser"
    doLast {
        def url = "http://127.0.0.1:8000/"
        if (isWin) {
            exec { commandLine 'cmd', '/c', "start $url" }
        } else {
            exec { commandLine "open", url }
        }
        exec { commandLine("mkdocs", "serve") }
    }
}