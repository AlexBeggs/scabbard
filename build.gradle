buildscript {
    apply from: 'constants.gradle'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${versions.agp}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "dev.arunkumar:scabbard-gradle-plugin:${publishVersion}"
    }
}

apply from: "gradle/local-properties.gradle"

allprojects {
    repositories {
        google()
        jcenter()
    }
    group groupId
    version publishVersion
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

//TODO Investigate migrating to GradleBuild. ATM has issues disabling daemon.
tasks.register("runScabbardProcessor" /*, GradleBuild*/) {
    def sample = "scabbard-sample"
    doLast {
        def isDebug = project.hasProperty("debug")
        if (isDebug) {
            delete fileTree("${sample}/build").matching {
                include "**/*.dot"
                include "**/*.png"
            }
        }
        exec {
            def isWin = System.getProperty("os.name").toLowerCase().contains("win")
            def shell = isWin ? 'cmd' : 'sh'
            def commandPrefix = isWin ? '' : './'
            def shellArg = isWin ? '/c' : '-c'

            def gradleDebug = "-Dorg.gradle.debug=$isDebug"
            def daemon = isDebug ? '--no-daemon' : ''
            def command = "${commandPrefix}gradlew $daemon $gradleDebug ${sample}:kaptDebugKotlin"
            workingDir(projectDir)
            commandLine shell, shellArg, command
        }
    }
}