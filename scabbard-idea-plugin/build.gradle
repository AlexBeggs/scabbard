import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
  id "org.jetbrains.intellij" version "1.0"
  id "org.jetbrains.kotlin.jvm"
}

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
  toolchain {
    // languageVersion = JavaLanguageVersion.of(11)
  }
}

tasks.withType(KotlinCompile).configureEach {
  kotlinOptions.jvmTarget = "1.8"
}

dependencies {
  testImplementation "junit:junit:${versions.junit}"
  testImplementation "com.google.truth:truth:${versions.truth}"
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
  updateSinceUntilBuild.set(false)

  version.set("201.8743.12") // AS 4.2.1
  type.set("IC")

  plugins.set([
    "Kotlin", "IntelliLang", "java", "android"
  ])
  pluginName.set("Scabbard - Dagger 2 Visualizer")
  // localPath.set("C:\\Users\\arunk\\AppData\\Local\\JetBrains\\Toolbox\\apps\\AndroidStudio\\ch-0\\202.7351085")
}

patchPluginXml {
  changeNotes.set(provider {
    """
      0.3.0
      New Gutter Icon, inherited bindings support in subcomponent graphs, and ability to open multiple images via gutter popup
      0.2.0
      Gutter icons on @ContributesAndroidInjector to open generated subcomponent image.
      <br>  
      Initial release - 0.1.0
      <br>
      Gutter icons on @Component, @Subcomponent and @Module to open graph generated by Scabbard Gradle plugin"""
  })
}

runIde {
  systemProperty "idea.ProcessCanceledException", "disabled"
}

publishPlugin {
  token.set(project.findProperty("intellijPublishToken"))
}
