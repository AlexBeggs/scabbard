plugins {
    id 'java-gradle-plugin'
    id "org.gradle.kotlin.kotlin-dsl" version "1.3.3"
}

apply from: '../constants.gradle'

repositories {
    jcenter()
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"

    testImplementation "junit:junit:${versions.junit}"
    testImplementation "com.google.truth:truth:${versions.truth}"
    testImplementation gradleTestKit()
}

gradlePlugin {
    plugins {
        greeting {
            id = 'scabbard-gradle-plugin'
            implementationClass = 'dev.arunkumar.scabbard.gradle.ScabbardGradlePlugin'
        }
    }
}

sourceSets {
    functionalTest {
        java {
            srcDirs 'src/functionalTest/kotlin'
        }
    }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

task functionalTest(type: Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

check {
    dependsOn(tasks.functionalTest)
}