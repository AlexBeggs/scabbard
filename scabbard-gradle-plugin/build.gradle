plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id "org.gradle.kotlin.kotlin-dsl" version "1.3.3"
}

apply from: '../constants.gradle'

group groupId
version publishVersion

repositories {
    jcenter()
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"

    testImplementation "junit:junit:${versions.junit}"
    testImplementation "com.google.truth:truth:${versions.truth}"
    testImplementation gradleTestKit()
}

gradlePlugin {
    plugins {
        scabbardPlugin {
            id = 'scabbard-gradle-plugin'
            displayName = 'Scabbard - Dagger 2 Visualizer'
            description = 'Gradle plugin to visualize Dagger 2 dependency graph'
            implementationClass = 'dev.arunkumar.scabbard.gradle.ScabbardGradlePlugin'
        }
    }
}

sourceSets {
    functionalTest {
        java {
            srcDirs 'src/functionalTest/kotlin'
        }
    }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

task functionalTest(type: Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

check {
    dependsOn(tasks.functionalTest)
}

pluginBundle {
    website = 'https://github.com/arunkumar9t2/scabbard'
    vcsUrl = 'https://github.com/arunkumar9t2/scabbard'
    tags = ['dagger 2', 'dependency injection', 'visualize dependency graph']
}